description = 'CodeFlux Common Android Stuff'
group = 'com.teamcodeflux.android'
version = '0.2.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

def compatibilityVersion = '1.6'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    mavenCentral()
    maven {
        url 'http://maven.springframework.org/milestone'
    }
}

configurations {
    provided
    provided.extendsFrom(compile)
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

def androidVersion = '2.1.2'
def springAndroidVersion = '1.0.0.M4'
def jacksonMapperVersion = '1.8.5'

def junitVersion = '4.10'
def mockitoVersion = '1.8.5'

dependencies {
    provided "com.google.android:android:$androidVersion"
    provided "org.springframework.android:spring-android-rest-template:$springAndroidVersion"
    provided "org.codehaus.jackson:jackson-mapper-asl:$jacksonMapperVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-all:$mockitoVersion"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true

        scopes.PROVIDED.plus += configurations.provided
    }
    project {
        javaVersion = compatibilityVersion
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file:///maven/release')
            pom.artifactId = 'fluxdroid'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    classpath += configurations.provided
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

assemble.dependsOn clean

defaultTasks 'assemble'
